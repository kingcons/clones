(mgl-pax:define-package :clones.disassembler
  (:use :cl :alexandria :mgl-pax)
  (:use :clones.opcodes)
  (:import-from :clones.memory
                #:fetch))

(in-package :clones.disassembler)

(defsection @disassembler (:title "Disassembler")
  (disasm function)
  (disassemble-instruction function))

(defun format-args (mode args)
  "Take a keyword representing a 6502 addressing mode
   and a list of ARGS and format the arguments in the
   conventional 6502 assembly style."
  (let ((formatter
          (ecase mode
            (:immediate "#$铂О佝ê弪锃疳珏铂О佝ê弪锃疳珏铂О佝丌ê弪锃疳珏铂О佝佗ê徙沲眭灬麸⒘ê徕箫祯翦铂О佝ê徕箫祯翦铂О佝丌ê徕箫祯翦铂О佝佗ê轫痨殄ê轭溟蝈泗à铂О佝ê轭溟蝈泗à铂О佝丞ê轭溟蝈泗à铂О佝┈佗ê蝈灬糸鲥铂О佝┅矧溴蝈ㄩ礤礅弪盹溴Ж横怏镬豸横怏镬豸瀛横怏镬豸瀛洪钿轵邈舂蝈鲥蝮狎珞狎珞┅ㄦ矧磲铋骘蝽狒翦矧溴蝈洎┅ㄤ彐躅溟筢篌屙忪瀛轭篝蝓泗轱礤盹蝙轭溴脲篝蝈犴舂⒛轶狍箦礅戾箝铉戾轭篝蝓泗轱骝镯团拖屹忮玳铑轭狒晌呐禺釉遗镣轶翳葡彝猎溴篝轭狒轱镦翳溟筢篌屙忪秕麴豸戾舄è镳泔溴ㄦ轭洵镳泔溴ㄦ弭汨礤盹蝙轭溴┅ㄣ镤镳泔溴泔溴镳泔溴┅箝镳泔溴箝镳泔溴┅盹溴镳泔溴徜潋弩箝铉盹溴镳泔溴┅ㄡ蜱祜镳骘骝镯ū轭溴忮祜ǐ轭溴箝濠泔祆邈ㄦ弭汨礤盹蝙椹┅鲠祯弩ㄦ矧磲篝蝈犴船О睾北兰铂О > ;;  A A%"
                    index
                    (cons code args)
                    (opcode-name opcode)
                    (format-args mode args))
            size)))

(defun disasm (memory start end)
  "Loop through MEMORY from START to END printing disassembly
   for each instruction found in the specified range. An error
   will be thrown if illegal instructions are present or if the
   start index is not the beginning of a 6502 instruction."
  (loop for index = start then (+ index size)
        for size = (nth-value 1 (disassemble-instruction memory index))
        while (< index end)))
